#!/usr/bin/env tclsh

package require Tk

################################################################################
# VARIABLES
#
namespace eval tClip {
  variable on_top    false
  variable word_wrap none
  variable clip      {}
  variable clip_list [list]
  variable msg       {}
  variable theme     [ttk::style theme use]
  variable type [expr {[tk windowingsystem] eq "x11" ? "UTF8_STRING" : "STRING"}]
}

################################################################################
# PROCEDURES
#
proc readFromClipboard {widget} {
  if {[catch {set tClip::clip [clipboard get -type $tClip::type]}]} {return 1}
  # do not update combobox list if already there
  if {$tClip::clip ni $tClip::clip_list} {
    lappend tClip::clip_list $tClip::clip
  }
  $widget delete 0.0 end
  $widget insert end $tClip::clip
}

proc addToClipboard {widget data} {
  if {$data eq ""} {return 1}
  clipboard clear
  clipboard append -type STRING -- $data
  readFromClipboard $widget
}

proc clearClipboard {widget} {
  clipboard clear
  $widget delete 0.0 end
}

proc undoRedo {widget command} {
  catch {$widget edit $command}
}

proc selectAll {widget} {
  if {[$widget get 0.0 "end -1 indices"] eq ""} {return 1}
  $widget tag add sel 0.0 "end -1 indices"
}

proc textModified {text} {
  .menu.clipboard entryconfigure "Add to Clipboard" \
    -state [expr {[$text edit modified] ? "normal" : "disabled"}]
}

proc undoStack {text} {
  foreach {item action} {Undo canundo Redo canredo} {
    .menu.edit entryconfigure $item \
      -state [expr {[$text edit $action] ? "normal" : "disabled"}]
  }
}

proc getPipedData {} {
  set data ""
  while {[gets stdin line] >= 0} {
    append data $line "\n"
  }
  chan event stdin readable {}
  set data [string range $data 0 end-1]
  if {[string length $data] > 0} {
    addToClipboard .text $data
  }
}

proc helpAbout {} {
  tk_messageBox -title "About tClip" -icon info -type ok -parent . \
    -message "tClip" -detail \
{A simple clipboard manager,
written in core Tcl/Tk.

MIT License

Copyright (c) Thanos Zygouris
<athanasios.zygouris@gmail.com>}
}

################################################################################
# MAIN MENU
#
proc createMenu {} {
  option add *tearOff false
  . configure -menu [menu .menu]
  .menu add cascade -label "Clipboard" -underline 0 -menu [menu .menu.clipboard]
    .menu.clipboard add command -label "Read from Clipboard" -underline 0 \
      -accelerator "Ctrl+R" -command {readFromClipboard .text}
    .menu.clipboard add command -label "Add to Clipboard" -underline 0 \
      -state disabled -accelerator "F3" \
      -command {addToClipboard .text [.text get 0.0 end-1char]}
    .menu.clipboard add command -label "Clear Clipboard" -underline 0 \
      -accelerator "Ctrl+L" -command {clearClipboard .text}
    .menu.clipboard add separator
    .menu.clipboard add command -label "Exit" -underline 1 \
      -accelerator "Ctrl+Q" -command {exit}
  .menu add cascade -label "Edit" -underline 0 -menu [menu .menu.edit]
    .menu.edit add command -label "Undo" -underline 0 \
      -accelerator "Ctrl+Z" -command {undoRedo .text undo}
    .menu.edit add command -label "Redo" -underline 0 \
      -accelerator "Ctrl+Shift+Z" -command {undoRedo .text redo}
    .menu.edit add separator
    .menu.edit add command -label "Cut" -underline 2 \
      -accelerator "Ctrl+X" -command {tk_textCut .text}
    .menu.edit add command -label "Copy" -underline 0 \
      -accelerator "Ctrl+C" -command {tk_textCopy .text}
    .menu.edit add command -label "Paste" -underline 0 \
      -accelerator "Ctrl+V" -command {tk_textPaste .text}
    .menu.edit add separator
    .menu.edit add command -label "Select All" -underline 7 \
      -accelerator "Ctrl+A" -command {selectAll .text}
  .menu add cascade -label "Options" -underline 0 -menu [menu .menu.options]
    .menu.options add checkbutton -label "On Top" -underline 0 \
      -accelerator "F2" \
      -variable tClip::on_top -onvalue "true" -offvalue "false" \
      -command {wm attributes . -topmost $tClip::on_top}
    .menu.options add separator
    .menu.options add checkbutton -label "Word Wrap" -underline 5 \
      -accelerator "Ctrl+W" \
      -variable tClip::word_wrap -onvalue "word" -offvalue "none" \
      -command {.text configure -wrap $tClip::word_wrap}
    .menu.options add separator
    .menu.options add cascade -label "Themes" -underline 0 \
      -menu [menu .menu.options.themes]
      foreach theme [ttk::style theme names] {
        .menu.options.themes add radiobutton -label $theme \
          -variable tClip::theme -value $theme \
          -command [list ttk::style theme use $theme]
      }
  .menu add cascade -label "Help" -underline 0 -menu [menu .menu.help]
    .menu.help add command -label "About tClip..." -underline 0 \
      -accelerator "F1" -command {helpAbout}
}

################################################################################
# WIDGETS
#
proc createWidgets {} {
  ttk::style configure flat.TLabelframe -relief flat

  ttk::labelframe .lf_clip -text "\u2022 Saved Clips:" -style flat.TLabelframe
    ttk::combobox .cb_clip -textvariable tClip::clip \
      -takefocus 0 -state readonly -exportselection true \
      -values tClip::clip_list \
      -postcommand {.cb_clip configure -values $tClip::clip_list}
    grid .cb_clip -in .lf_clip -row 0 -column 0 -sticky we
    grid columnconfigure .lf_clip .cb_clip -weight 1

  ttk::labelframe .lf_text -text "\u2022 Details:" -style flat.TLabelframe
    tk::text .text -relief sunken -highlightthickness 0 -takefocus 0 \
      -wrap $tClip::word_wrap -undo true -autoseparators true \
      -tabstyle wordprocessor -insertofftime 0 \
      -xscrollcommand {.xscroll set} -yscrollcommand {.yscroll set}
    ttk::scrollbar .yscroll -orient vertical   -command {.text yview}
    ttk::scrollbar .xscroll -orient horizontal -command {.text xview}
    grid .text    -in .lf_text -row 0 -column 0 -sticky nswe
    grid .yscroll -in .lf_text -row 0 -column 1 -sticky ns
    grid .xscroll -in .lf_text -row 1 -column 0 -sticky we
    grid rowconfigure    .lf_text .text -weight 1
    grid columnconfigure .lf_text .text -weight 1

  ttk::frame .statusbar -borderwidth 1 -relief sunken
    ttk::label .message -textvariable tClip::msg
    ttk::sizegrip .sizegrip
    grid .message  -in .statusbar -row 0 -column 0 -sticky we
    grid .sizegrip -in .statusbar -row 0 -column 1 -sticky e
    grid columnconfigure .statusbar .message -weight 1

  grid .lf_clip   -row 0 -column 0 -sticky we
  grid .lf_text   -row 1 -column 0 -sticky news
  grid .statusbar -row 2 -column 0 -sticky we
  grid rowconfigure    . .lf_text -weight 1
  grid columnconfigure . .lf_text -weight 1
}

################################################################################
# BINDINGS
#
proc createBindings {} {
  set bindings [list \
    <Control-r> \
    <Control-l> \
    <Control-q> \
    <Control-z> \
    <Control-Z> \
    <Control-a> \
    <Control-w> \
  ]
  foreach keysym $bindings {
    bind Text $keysym {return 0}
  }

  bind all <KP_Enter>  {event generate %W <Return>}
  bind all <Control-r> {.menu.clipboard invoke "Read from Clipboard"}
  bind all <F3>        {.menu.clipboard invoke "Add to Clipboard"}
  bind all <Control-l> {.menu.clipboard invoke "Clear Clipboard"}
  bind all <Control-q> {.menu.clipboard invoke "Exit"}
  bind all <Control-z> {.menu.edit invoke "Undo"}
  bind all <Control-Z> {.menu.edit invoke "Redo"}
  bind all <Control-a> {.menu.edit invoke "Select All"}
  bind all <F2>        {.menu.options invoke "On Top"}
  bind all <Control-w> {.menu.options invoke "Word Wrap"}
  bind all <F1>        {.menu.help invoke "About tClip..."}

  bind Text <<Modified>>  {textModified %W}
  bind Text <<UndoStack>> {undoStack %W}
  bind Text <ButtonPress-3> {focus %W; tk_popup .menu.edit %X %Y}

  bind TCombobox <<ComboboxSelected>> {addToClipboard .text $tClip::clip}
}

################################################################################
# MAIN PROGRAM
#
createMenu
createWidgets
createBindings
focus .text
# initialize undo/redo menu items
undoStack .text
# check clipboard on program start
readFromClipboard .text

wm title    . "tClip"
wm minsize  . 300 250
wm protocol . WM_DELETE_WINDOW {.menu.clipboard invoke "Exit"}

################################################################################
# COMMAND LINE
#
if {$::argc > 0} {
  addToClipboard .text [join $::argv "\n"]
}
# get input from OS pipe (stdout)
chan event stdin readable {getPipedData}

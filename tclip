#!/usr/bin/env tclsh

package require Tk

################################################################################
# VARIABLES
#
namespace eval tClip {
  variable version  {0.4.6}
  variable auto     {true}
  variable ontop    {false}
  variable wrap     {none}
  variable clip     {}
  variable hclip    [list]
  variable clips    {0 clips}
  variable words    {0 words}
  variable bytes    {0 bytes}
  variable encoding [encoding system]
  variable theme    [ttk::style theme use]
  variable type     [expr {[tk windowingsystem] eq "x11" ? "UTF8_STRING" : "STRING"}]
}

################################################################################
# PROCEDURES
#
proc tClip::Message {msg} {
  set words 0
  foreach word [regexp -all -inline {[[:alnum:]]+} $msg] {
    incr words
  }
  set tClip::clips [list [set clips [llength $tClip::hclip]] \
    [expr {$clips eq 1 ? "clip" : "clips"}]]
  set tClip::words [list $words [expr {$words eq 1 ? "word" : "words"}]]
  set tClip::bytes [list [set bytes [string length [encoding convertto utf-8 $msg]]] \
    [expr {$bytes eq 1 ? "byte" : "bytes"}]]
}

proc tClip::Read {} {
  try {
    set tClip::clip [clipboard get -type $tClip::type]
  } on error msg {
    # do nothing
  } on ok msg {
    # update combobox list if not listed already
    if {$tClip::clip ni $tClip::hclip} {
      lappend tClip::hclip $tClip::clip
    }
    tClip::Message $tClip::clip
  } finally {
    # cancel any rogue previous timers
    after cancel {tClip::Read}
    if {$tClip::auto eq "true"} {
      after 1000 {tClip::Read}
    }
  }
}

proc tClip::Update {{text ".text"}} {
  tClip::Read
  set old [$text get 0.0 "end -1 indices"]
  set oldcursor [$text index insert]
  $text delete 0.0 end
  $text insert end $tClip::clip
  set new [$text get 0.0 "end -1 indices"]
  # in user input keep cursor in place
  if {[string equal $old $new] == 1} {
    $text mark set insert $oldcursor
  } else {
    $text mark set insert 0.0
    $text see insert
  }
  focus $text
}

proc tClip::Add {data} {
  if {$data eq ""} {return 1}
  clipboard clear
  clipboard append -type STRING -- $data
  tClip::Update
}

proc tClip::Clear {{text ".text"}} {
  clipboard clear
  $text delete 0.0 end
}

proc tClip::SelectAll {{text ".text"}} {
  if {[$text get 0.0 "end -1 indices"] eq ""} {return 1}
  $text tag add sel 0.0 "end -1 indices"
}

proc tClip::Modified {{text ".text"}} {
  .menu.clipboard entryconfigure "Add to Clipboard" \
    -state [expr {[$text edit modified] ? "normal" : "disabled"}]
}

proc tClip::UndoStack {{text ".text"}} {
  foreach {item action} {Undo canundo Redo canredo} {
    .menu.edit entryconfigure $item \
      -state [expr {[$text edit $action] ? "normal" : "disabled"}]
  }
}

proc tClip::Pipe {} {
  chan event stdin readable {}
  set data ""
  while {[chan gets stdin line] >= 0} {
    append data $line "\n"
  }
  set data [string range $data 0 end-1]
  if {[string length $data] > 0} {
    tClip::Add $data
  }
}

proc tClip::System {} {
  tk_messageBox -title "System Information" -icon info -type ok -parent . \
    -message "Tcl/Tk Version: [info patchlevel]" \
    -detail "\
      User Name:\t${::tcl_platform(user)}\n\
      Hostname:\t[info hostname]\n\
      OS Family:\t${::tcl_platform(platform)}\n\
      OS Identifier:\t${::tcl_platform(os)}\n\
      OS Version:\t${::tcl_platform(osVersion)}\n\
      Architecture:\t${::tcl_platform(machine)}\n\
      Window System:\t[tk windowingsystem]\n\
      Encoding:\t[encoding system]"
}

proc tClip::About {version} {
  tk_messageBox -title "About tClip" -icon info -type ok -parent . \
    -message "tClip $version" -detail \
{A simple clipboard manager,
written in core Tcl/Tk.

MIT License

Copyright Â© Thanos Zygouris
<athanasios.zygouris@gmail.com>}
}

################################################################################
# MAIN MENU
#
proc tClip::Menu {} {
  option add *tearOff false
  . configure -menu [menu .menu]
  .menu add cascade -label "Clipboard" -underline 0 -menu [menu .menu.clipboard]
    .menu.clipboard add command -label "Read from Clipboard" -underline 0 \
      -accelerator "Ctrl+R" -command {tClip::Update}
    .menu.clipboard add command -label "Add to Clipboard" -underline 0 \
      -state disabled -accelerator "F3" \
      -command {tClip::Add [.text get 0.0 "end -1 indices"]}
    .menu.clipboard add command -label "Clear Clipboard" -underline 0 \
      -accelerator "Ctrl+L" -command {tClip::Clear}
    .menu.clipboard add separator
    .menu.clipboard add command -label "Exit" -underline 1 \
      -accelerator "Ctrl+Q" -command {exit}
  .menu add cascade -label "Edit" -underline 0 -menu [menu .menu.edit]
    .menu.edit add command -label "Undo" -underline 0 \
      -accelerator "Ctrl+Z" -command {.text edit undo}
    .menu.edit add command -label "Redo" -underline 0 \
      -accelerator "Ctrl+Shift+Z" -command {.text edit redo}
    .menu.edit add separator
    .menu.edit add command -label "Cut" -underline 2 \
      -accelerator "Ctrl+X" -command {tk_textCut .text}
    .menu.edit add command -label "Copy" -underline 0 \
      -accelerator "Ctrl+C" -command {tk_textCopy .text}
    .menu.edit add command -label "Paste" -underline 0 \
      -accelerator "Ctrl+V" -command {tk_textPaste .text}
    .menu.edit add separator
    .menu.edit add command -label "Select All" -underline 7 \
      -accelerator "Ctrl+A" -command {tClip::SelectAll}
  .menu add cascade -label "Options" -underline 0 -menu [menu .menu.options]
    .menu.options add checkbutton -label "On Top" -underline 3 \
      -accelerator "F2" \
      -variable tClip::ontop -onvalue "true" -offvalue "false" \
      -command {wm attributes . -topmost $tClip::ontop}
    .menu.options add separator
    .menu.options add checkbutton -label "Word Wrap" -underline 5 \
      -accelerator "Ctrl+W" \
      -variable tClip::wrap -onvalue "word" -offvalue "none" \
      -command {.text configure -wrap $tClip::wrap}
    .menu.options add separator
    .menu.options add cascade -label "Theme" -underline 3 \
      -menu [menu .menu.options.themes]
      foreach theme [ttk::style theme names] {
        .menu.options.themes add radiobutton -label $theme \
          -variable tClip::theme -value $theme \
          -command [list ttk::style theme use $theme]
      }
    .menu.options add separator
    .menu.options add checkbutton -label "Auto Update" -underline 5 \
      -variable tClip::auto -onvalue "true" -offvalue "false" \
      -command {tClip::Read}
  .menu add cascade -label "Help" -underline 0 -menu [menu .menu.help]
    .menu.help add command -label "System..." -underline 0 \
      -command {tClip::System}
    .menu.help add separator
    .menu.help add command -label "About tClip..." -underline 0 \
      -accelerator "F1" -command {tClip::About $tClip::version}
}

################################################################################
# WIDGETS
#
proc tClip::Widgets {} {
  ttk::labelframe .lf_clip -relief flat -text "\u2022 Clips:"
    ttk::combobox .cb_clip -textvariable tClip::clip \
      -takefocus 0 -state readonly -exportselection true \
      -values $tClip::hclip \
      -postcommand {.cb_clip configure -values $tClip::hclip}
    grid .cb_clip -in .lf_clip -row 0 -column 0 -sticky we
    grid columnconfigure .lf_clip .cb_clip -weight 1

  ttk::labelframe .lf_text -relief flat -text "\u2022 Details:"
    tk::text .text -relief sunken -highlightthickness 0 -takefocus 0 \
      -wrap $tClip::wrap -undo true -autoseparators true \
      -tabstyle wordprocessor -insertofftime 0 \
      -xscrollcommand {.xscroll set} -yscrollcommand {.yscroll set}
    ttk::scrollbar .yscroll -orient vertical   -command {.text yview}
    ttk::scrollbar .xscroll -orient horizontal -command {.text xview}
    grid .text    -in .lf_text -row 0 -column 0 -sticky nswe
    grid .yscroll -in .lf_text -row 0 -column 1 -sticky ns
    grid .xscroll -in .lf_text -row 1 -column 0 -sticky we
    grid rowconfigure    .lf_text .text -weight 1
    grid columnconfigure .lf_text .text -weight 1

  ttk::frame .statusbar -borderwidth 1 -relief sunken
    ttk::label .clips    -anchor w -textvariable tClip::clips
    ttk::label .words    -anchor w -textvariable tClip::words
    ttk::label .bytes    -anchor w -textvariable tClip::bytes
    ttk::label .encoding -anchor e -textvariable tClip::encoding
    ttk::sizegrip .sizegrip
    grid .clips    -in .statusbar -row 0 -column 0 -sticky we
    grid .words    -in .statusbar -row 0 -column 1 -sticky we
    grid .bytes    -in .statusbar -row 0 -column 2 -sticky we
    grid .encoding -in .statusbar -row 0 -column 3 -sticky e
    grid .sizegrip -in .statusbar -row 0 -column 4 -sticky e
    grid columnconfigure .statusbar .clips -weight 1
    grid columnconfigure .statusbar .words -weight 1
    grid columnconfigure .statusbar .bytes -weight 1

  grid .lf_clip   -row 0 -column 0 -sticky we
  grid .lf_text   -row 1 -column 0 -sticky news
  grid .statusbar -row 2 -column 0 -sticky we
  grid rowconfigure    . .lf_text -weight 1
  grid columnconfigure . .lf_text -weight 1
}

################################################################################
# BINDINGS
#
proc tClip::Bindings {} {
  set bindings [list \
    <Control-a> <Control-l> <Control-q> <Control-r> \
    <Control-w> <Control-z> <Control-Z> \
  ]
  foreach keysym $bindings {
    bind Text $keysym {return 0}
  }

  bind all <KP_Enter>  {event generate %W <Return>}
  bind all <Control-r> {.menu.clipboard invoke "Read from Clipboard"}
  bind all <F3>        {.menu.clipboard invoke "Add to Clipboard"}
  bind all <Control-q> {.menu.clipboard invoke "Exit"}
  bind all <F2>        {.menu.options invoke "On Top"}
  bind all <F1>        {.menu.help invoke "About tClip..."}

  bind Text <Control-l>     {.menu.clipboard invoke "Clear Clipboard"}
  bind Text <Control-z>     {.menu.edit invoke "Undo"}
  bind Text <Control-Z>     {.menu.edit invoke "Redo"}
  bind Text <Control-a>     {.menu.edit invoke "Select All"}
  bind Text <Control-w>     {.menu.options invoke "Word Wrap"}
  bind Text <<Modified>>    {tClip::Modified %W}
  bind Text <<UndoStack>>   {tClip::UndoStack %W}
  bind Text <ButtonPress-3> {tk_popup .menu.edit %X %Y}

  bind TCombobox <<ComboboxSelected>> {
    tClip::Add $tClip::clip
    %W selection clear
  }
}

################################################################################
# MAIN PROGRAM
#
tClip::Menu
tClip::Widgets
tClip::Bindings
focus .text
# initialize undo/redo menu items
tClip::UndoStack
# check clipboard on program start
tClip::Update

wm title    . "tClip"
wm minsize  . 230 230
wm protocol . WM_DELETE_WINDOW {.menu.clipboard invoke "Exit"}

################################################################################
# COMMAND LINE
#
if {$::argc > 0} {
  tClip::Add [join $::argv "\n"]
}
# get input from OS pipe (stdout)
chan event stdin readable {tClip::Pipe}
